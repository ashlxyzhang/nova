#version 450

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(std430, set = 0, binding = 0) readonly buffer EventBuffer {
    vec4 data[];
} event_buffer;

layout(set = 1, binding = 0, rgba8) uniform image2D out_image;
layout(set = 1, binding = 1, r32ui) uniform uimage2D positive_texture;
layout(set = 1, binding = 2, r32ui) uniform uimage2D negative_texture;

layout(set = 2, binding = 0) uniform Uniforms {
    vec4 posCol;
    vec4 neutCol;
    vec4 negCol;
    vec4 floatFlags; // x: color option, y: scale, z: activation function, w: time center
    vec4 flags;      // x: posOnly?, y: morlet (unused)
    vec4 morletParams; // x: frequency, y: width (h), z: time center
} ubo;

float get_weight(float time){
    if (ubo.flags.y < 0.5) {
        return 1.0;
    }
    float tDiff = time - ubo.morletParams.z;
    float sinusoid = cos(2 * 3.14159265 * ubo.morletParams.x * tDiff);
    float gauss = exp(- 4 * 0.693147 * (tDiff * tDiff) / (ubo.morletParams.y * ubo.morletParams.y));
    //frequency domain bandwidth
    // float gauss = exp(3.14159265 * 3.14159265 * ubo.morletParams.y * ubo.morletParams.y *tDiff * tDiff / (4 * 0.693147));
    return sinusoid * gauss;

}
void write_to_intermediate_texture(int eventIndex) {
    vec4 evt = event_buffer.data[eventIndex];
    float x = evt.x;
    float y = evt.y;
    float polarity = evt.w;
    float time = evt.z / 1000; // unused

    // Convert to ivec2 coords; ensure these coords are valid for your texture at dispatch time
    ivec2 coord = ivec2(int(x), int(y));

    float weight = get_weight(time);

    bool is_positive = (polarity == 1.00);
    bool posWeight = (weight > 0.0);
    weight = abs(weight);
    uint intWeight = uint(100000*weight);
    

    if (is_positive == posWeight) {
        imageAtomicAdd(positive_texture, coord, intWeight);
    } else if(ubo.flags.x < 0.5) {
        imageAtomicAdd(negative_texture, coord, intWeight);
    }
}

void main() {
    uvec3 gid = gl_GlobalInvocationID;

    // explicit cast from uint -> int
    write_to_intermediate_texture(int(gid.x));
}
